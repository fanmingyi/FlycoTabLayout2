apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

def siteUrl = 'https://github.com/fanmingyi/FlycoTabLayout2'  // 项目的主页
def gitUrl = 'https://github.com/fanmingyi/FlycoTabLayout2'   // Git仓库的url
def mGroup = "com.fmy.tablayout"       // Maven Group ID for the artifact，一般填你唯一的包名
def mArtifactId = "tablayout2"       // Maven Group ID for the artifact，一般填你唯一的包名

// 这个version是区分library版本的，因此当我们需要更新library时记得修改这个version
def mVersion = "1.0.0"
android {
    compileSdkVersion mCompileSdkVersion
    buildToolsVersion mBuildToolsVersion

    defaultConfig {
        minSdkVersion mMinSdkVersion
        targetSdkVersion mTargetSdkVersion
        versionCode 212
        versionName mVersion
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation "androidx.viewpager2:viewpager2:1.0.0"

}


afterEvaluate {
    task(type:Jar,"sourcesJar"){
        from android.sourceSets.main.java.srcDirs
        classifier "sources"
    }
    task javadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
        destinationDir = file("../javadoc/")
        failOnError false
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release
                // You can then customize attributes of the publication as shown below.
                groupId = mGroup
                artifactId = mArtifactId
                version = mVersion
                artifact sourcesJar
                artifact javadocJar
                // Define this explicitly if using implementation or api configurations
                pom.withXml {
                    def dependenciesNode = asNode().getAt('dependencies')[0] ?: asNode().appendNode('dependencies')

                    // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                    configurations.implementation.allDependencies.each {
                        // Ensure dependencies such as fileTree are not included.
                        if (it.name != 'unspecified') {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                        }
                    }
                }
            }

        }
    }
}



bintray {
    user = "fanmingyicode"
    key = ''
    publications = ['release']
    pkg {
        repo = "maven"
        name = "FlycoTabLayout2"    //发布到JCenter上的项目名字
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ['Apache-2.0']

        version {
            name = mVersion
            desc = '1.Migrate Android. 2.Support ViewPager2'
            released  = new Date()
            vcsTag = mVersion
            attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
        }
    }
}
